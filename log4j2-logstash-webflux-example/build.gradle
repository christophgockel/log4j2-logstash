plugins {
    id 'com.github.spotbugs'
    id 'org.springframework.boot'
    id 'java'
}

configurations.all {
    resolutionStrategy {
        failOnVersionConflict()
        // From https://search.maven.org/artifact/org.apache.logging.log4j/log4j-slf4j-impl/2.14.1/jar
        //     <!-- Do not upgrade the SLF4J version. 1.7.26 broke backward compatibility. Users can update the version if
        //      they do not require support for SLF4J's EventData -->
        dependencySubstitution {
            substitute module('org.slf4j:slf4j-api:1.7.32') with module('org.slf4j:slf4j-api:1.7.25')
        }
    }
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    exclude module: 'spring-boot-starter-logging'
    exclude module: 'nio-multipart-parser'
}

compileJava {
    options.compilerArgs << "-Xlint:all,-processing" << "-Werror"
}

compileTestJava {
    options.compilerArgs << "-Xlint:all,-processing" << "-Werror"
}

test {
    useJUnitPlatform()
    testLogging {
        events = ['failed', 'skipped']
    }
}

dependencies {
    constraints {
        api("com.github.spotbugs:spotbugs-annotations:${spotbugsVersion}")
    }

    implementation("com.github.spotbugs:spotbugs-annotations")
    implementation(enforcedPlatform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}"))

    implementation("com.lmax:disruptor:${disruptor}")

    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('org.springframework.boot:spring-boot-starter-webflux')
    implementation('org.springframework.boot:spring-boot-starter-log4j2')

    implementation(project(":log4j2-logstash-layout"))
    implementation(project(":log4j2-logstash-taggedmessage"))
    implementation(project(":log4j2-logstash-reactor"))

}

