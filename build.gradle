plugins {
    id 'com.github.ben-manes.versions' version '0.29.0'
    id 'com.github.spotbugs' version '4.5.0' apply false
}

group 'se.fnord'
version '0.6-SNAPSHOT'

ext {
    isTravis = System.getenv('TRAVIS') ? true : false
    isRelease = !isTravis && !version.endsWith("SNAPSHOT")

    ossrhUsername = System.getenv('OSSRH_USERNAME')
    ossrhPassword = System.getenv('OSSRH_PASSWORD')
}

apply plugin: 'java-platform'

javaPlatform {
    allowDependencies()
}

dependencies {
    constraints {
        api("net.javacrumbs.json-unit:json-unit:${jsonUnit}")
        api("net.javacrumbs.json-unit:json-unit-fluent:${jsonUnit}")
    }

    api(enforcedPlatform("org.apache.logging.log4j:log4j-bom:${log4j}"))
    api(enforcedPlatform("com.fasterxml.jackson:jackson-bom:${jackson}"))
    api(enforcedPlatform("org.junit:junit-bom:${junit5}"))
}


subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'com.github.spotbugs'

    task javadocJar(type: Jar) {
        archiveClassifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        archiveClassifier = 'sources'
        from sourceSets.main.allJava
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        mavenCentral()
    }

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
        }
    }

    compileJava {
        options.compilerArgs << "-Xlint:all,-processing" << "-Werror"
    }

    compileTestJava {
        options.compilerArgs << "-Xlint:all,-processing" << "-Werror"
    }

    signing {
        required { isRelease && gradle.taskGraph.hasTask(":uploadArchives") }
        sign configurations.archives
    }

    test {
        useJUnitPlatform()
        testLogging {
            events = ['failed', 'skipped']
        }
    }

    dependencies {
        api(enforcedPlatform(rootProject))
        implementation(enforcedPlatform(rootProject))
        testImplementation(enforcedPlatform(rootProject))
        annotationProcessor(enforcedPlatform(rootProject))
        testAnnotationProcessor(enforcedPlatform(rootProject))
    }
}

wrapper {
    gradleVersion = '6.6'
}

project(':log4j2-tagged-message') {
    dependencies {
        api "org.apache.logging.log4j:log4j-api"

        testImplementation "org.junit.jupiter:junit-jupiter-api"
        testImplementation "org.junit.jupiter:junit-jupiter-params"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                pom.project {
                    name project.name
                    packaging 'jar'
                    description 'Tagged message type for Log4J2'
                    url 'https://github.com/log4j2-logstash'

                    scm {
                        url 'scm:git:https://github.com/gsson/log4j2-logstash'
                        connection 'scm:git:git@github.com:/gsson/log4j2-logstash'
                        developerConnection 'scm:git:git@github.com:/gsson/log4j2-logstash'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'gsson'
                            name 'Henrik Gustafsson'
                        }
                    }
                }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
            }
        }
    }
}


project(':log4j2-logstash-layout') {
    dependencies {
        api "org.apache.logging.log4j:log4j-api"
        api project(':log4j2-tagged-message')

        implementation "org.apache.logging.log4j:log4j-core"
        annotationProcessor "org.apache.logging.log4j:log4j-core"
        testAnnotationProcessor "org.apache.logging.log4j:log4j-core"

        testImplementation "org.junit.jupiter:junit-jupiter-api"
        testImplementation "org.junit.jupiter:junit-jupiter-params"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"

        testImplementation "com.fasterxml.jackson.core:jackson-core"
        testImplementation "com.fasterxml.jackson.core:jackson-databind"
        testImplementation "net.javacrumbs.json-unit:json-unit"
        testImplementation "net.javacrumbs.json-unit:json-unit-fluent"
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                pom.project {
                    name project.name
                    packaging 'jar'
                    description 'Logstash json event layout for Log4J2'
                    url 'https://github.com/gsson/log4j2-logstash'

                    scm {
                        url 'scm:git:https://github.com/gsson/log4j2-logstash'
                        connection 'scm:git:git@github.com:/gsson/log4j2-logstash'
                        developerConnection 'scm:git:git@github.com:/gsson/log4j2-logstash'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'gsson'
                            name 'Henrik Gustafsson'
                        }
                    }
                }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
            }
        }
    }
}